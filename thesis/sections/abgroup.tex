\chapter{Abelian Group (Addition)}
\label{ch:abgroup}

\section{Addition Operation}
To define addition, we will use our iterator. For this, we will need an equivalence, in this simpler case we will first define an isomorphism, then convert that to an equivalence.
This isomorphism will be based on 'succ' being the function, its inverse function will be 'pred', and to prove that using 'succ' then 'pred', and using 'pred' then 'succ' results in the same value, we will use our 'section' and 'retraction' constructors.

We can then convert this isomorphism to an equivalence with the 'isoToEquiv' builtin function. Equivalence is an ordered pair, where the first parameter will be our 'succ' function, and the second will contain the proofs we previously mentioned.

Using the iterator we defined earlier, we can first supply the 'idfun' (identity function) version of ℤₕ, this will convert ℤₕ to a 'ℤₕ → ℤₕ' type. This will be beneficial since we have 2 implicit parameters, and we will want to move the constructors ('succ' and 'pred') from the left parameter to the right one. Next, we will post-compose our defined 'succEquiv'. This will change our 'ℤₕ ≃ ℤₕ' to a '(ℤₕ → ℤₕ) ≃ (ℤₕ → ℤₕ)' (more specifically (C → ℤₕ) ≃ (C → ℤₕ)), this is to match the type needed by our iterator. We implicitly supply the left and right parameters to this. (More specifically, we will first supply our left parameter, for this our iterator will return ℤₕ → ℤₕ, and we will supply our right parameter to this - see the comment in the code for this).

This will have the desired effect that we wanted: from our left parameter the 'succ' and 'pred' constructors will be placed on the beginning of the right parameter, which is otherwise known as addition.

As we already established when defining our iterator, we won't have to deal with our 1- and 2-dimensional construcotrs and their boundaries, as it is handled by our iterator.

\begin{minted}{agda}
succIso : Iso ℤₕ ℤₕ
succIso .Iso.fun      = succ
succIso .Iso.inv      = pred
succIso .Iso.rightInv = ret
succIso .Iso.leftInv  = sec

succEquiv : ℤₕ ≃ ℤₕ
succEquiv = isoToEquiv succIso

infixl 6 _+_
_+_ : ℤₕ → ℤₕ → ℤₕ
_+_ = ℤₕ-ite (idfun ℤₕ) (postCompEquiv succEquiv)
-- m + n = (ℤₕ-ite (idfun ℤₕ) (postCompEquiv succEquiv) m) n
\end{minted}

Definitionally, the following hold true for the addition operation in Agda:
\begin{minted}{agda}
zero   + n ≡ n
succ m + n ≡ succ (m + n)
pred m + n ≡ pred (m + n)
\end{minted}
Note that the symmetric version of these also hold true.

Alternatively, we could define addition by pattern matching on the second parameter, this would mean we manually have to satisfy the boundaries in the 1- and 2-dimensional constructor cases, however, here they would be fairly trivial:
% !!BOUNDARY!!
\begin{minted}{agda}
_+_ : ℤₕ → ℤₕ → ℤₕ
zero      + b = b
succ a    + b = succ (a + b)
pred a    + b = pred (a + b)
sec a i   + b = sec (a + b) i
ret a i   + b = ret (a + b) i
coh a i j + b = coh (a + b) i j
\end{minted}

\section{Associativity}
\begin{theorem}
  Addition is associative: ∀ m, n, o ∈ ℤ: m + (n + o) = (m + n) + o
\end{theorem}

\begin{proof}
  We will use our induction property to prove this theorem. For the first case, which states that this is indeed a property, we will use the 'isProp' (specifically the one which takes 2 parameters) and our set property, thankfully cubical Agda can infer the parameters for this. In all future proofs we will rely on Agda inferring these parameters.

  For the base case (m = 0) : 0 + (n + o) = (n + o) = (0 + n) + o is a reflection.

  For the succ case: (succ m) + (n + o) = ((succ m) + n) + o, note that since we are using the induction property, we will have the original m + (n + o) = (m + n) + o equality, where we are free to change the parameters of 'n' and 'o' as we please. Applying succ to the original equality will result in succ (m + (n + o)) = succ (m + n + o), thanks to the fact that succ (m + n) = succ m + n holds true definitionally, where we can supply 'm' to the m parameter, and 'n + o' to the n parameter. We will also have to note that addition is parenthesized from the left, so succ m + n + o = (succ m + n) + o is also definitionally true.

  For the pred case: (pred m) + (n + o) = ((pred m) + n) + o, similarly to the succ case, it is enough to apply pred to the original equality. We will once again note that pred (m + n) = pred m + n is definitionally true, and the fact that addition is parenthesized from the left.
\end{proof}

\begin{listing}[H]
\begin{minted}{agda}
+-assoc : ∀ m n o → m + (n + o) ≡ (m + n) + o
+-assoc = ℤₕ-ind-prop
  (λ _ → isPropΠ2 λ _ _ → isSetℤₕ _ _)
  (λ n o → refl)
  (λ m p n o → cong succ (p n o))
  (λ m p n o → cong pred (p n o))
\end{minted}
\caption{Agda proof of addition being associative}
\end{listing}

\section{Identity Element}
Before proving the existence of an identity element, we note that in the case of addition, both left and right identity element will be 0.
\begin{theorem}
  Left identity element exists for addition: ∃ id ∈ ℤ, ∀ z ∈ ℤ: id + z = z
\end{theorem}

\begin{proof}
  In Agda, we will rewrite the statement as such: ∀ z ∈ ℤ → 0 + z = z, since we know that the identity element is 0.

  The equation 0 + z = z is a reflection in Agda.
\end{proof}

\begin{listing}[H]
\begin{minted}{agda}
+-idˡ : ∀ z → zero + z ≡ z
+-idˡ z = refl
\end{minted}
\caption{Agda proof of addition having a left identity element}
\end{listing}

\begin{theorem}
  Right identity element exists for addition: ∃ id ∈ ℤ, ∀ z ∈ ℤ: z + id = z
\end{theorem}
(Interesting note: the existence of a right identity element, more specifically the Agda rewritten version of 'z + zero = z' is a Peano axiom\cite{peano}.)

\begin{proof}
  As in the case of the left identity element, we will rewrite this statement in Agda as such: ∀ z ∈ ℤ → z + 0 = z. We will once again use our induction property for proving this in Agda.

  For the base case (z = 0): 0 + 0 = 0 is a reflection.

  For the succ case: (succ z) + 0 = succ z, since we have the original equality of z + 0 = z, it is enough to apply succ on both sides of the original equality.

  For the pred case: (pred z) + 0 = pred z, similarly to the succ case, it is enough to apply pred on both sides of the original equality.
\end{proof}

\begin{listing}[H]
\begin{minted}{agda}
+-idʳ : ∀ z → z + zero ≡ z
+-idʳ = ℤₕ-ind-prop
  (λ _ → isSetℤₕ _ _)
  refl
  (λ z p → cong succ p)
  (λ z p → cong pred p)
\end{minted}
\caption{Agda proof of addition having a right identity element}
\end{listing}

\section{Negation and Subtraction Operations}
Before proving the existence of an inverse element, we will have to define subtraction. To do so, we will define the negation operation, since subtraction is definitionally equal to adding the negated element: 'm - n = m + (- n)'.

To define negation, we will once again use our iterator just like in the case of addition.

Our method will be similar to addition as well, we will place the constructors of our element in an inverted matter (converting our equivalence using 'invEquiv', since we are required to give both proofs of equivalence) on the 'zero' base element. So when our number contains a 'pred' we will place a 'succ' on 'zero' instead, and vice-versa, if our number contains a 'succ' we will place a 'pred' on 'zero'. This will have the desired effect of resulting in the inverted original number.

\begin{minted}{agda}
-_ : ℤₕ → ℤₕ
-_ = ℤₕ-ite zero (invEquiv succEquiv)
-- - n = ℤₕ-ite zero (invEquiv succEquiv) n

infixl 6 _-_
_-_ : ℤₕ → ℤₕ → ℤₕ
m - n = m + (- n)
\end{minted}

Definitionally, the following hold true for the negation operation in Agda:
\begin{minted}{agda}
- zero     ≡ zero
- (succ m) ≡ pred (- m)
- (pred m) ≡ succ (- m)
\end{minted}
Note that the symmetric versions of these also hold true.

Alternatively, we could once again define negation by pattern matching. In this case however, for the 'sec' constructor we would have to provide 'ret' to satisfy the boundaries, for the 'ret' constructor we would have to provide 'sec' to satisfy its boundaries. Interestingly for the 'coh' constructor, we would have to provide the previously defined 'hoc' to satisfy the boundaries.
% !!BOUNDARIES!!
\begin{minted}{agda}
negate : ℤₕ → ℤₕ
negate zero        = zero
negate (succ z)    = pred (negate z)
negate (pred z)    = succ (negate z)
negate (sec z i)   = ret (negate z) i
negate (ret z i)   = sec (negate z) i
negate (coh z i j) = hoc (negate z) i j
\end{minted}

\section{Inverse Element}
We note that in the case of addition, both left and right inverse element will be the negated number.
For proving the existence of left and right inverse elements, we will first prove 2 helper theorems:

\begin{theorem}
  The succ constructor can be moved around addition: ∀ m, n ∈ ℤ: m + succ n = succ (m + n)
\end{theorem}

\begin{proof}
  Using our induction property:

  For the base case (m = 0): 0 + (succ n) = succ (0 + n) is a reflection.

  For the succ case: (succ m) + (succ n) = succ ((succ m) + n), since we have the original equality of m + (succ n) = succ (m + n), it is enough to apply succ on both sides of the original equality, this results in succ (m + succ n) = succ (succ (m + n)), thanks to the fact that succ (m + n) = succ m + n holds definitionally, this case is proven.

  For the pred case: (pred m) + (succ n) = succ ((pred m) + n), since we have the original equality, we can apply pred on both sides of it. Unfortunately, this won't be enough, since this way we have the equation of pred (m + succ n) = pred (succ (m + n)), our right-hand sides don't match. We will have to use transitivity to change the right-hand side of the equation. We know that our section constructor states that pred (succ m) = m, applying this with 'm + n' gets rid of the pred (succ (...)) part, resulting in pred (m + succ n) = m + n. Using another transitivity, we will have to somehow put a succ (pred (...)) to this 'm + n'. This is just going to be the symmetric version of our retraction constructor, which states that succ (pred (m)) = m. This results in pred (m + succ n) = succ (pred (m + n)), thanks to the fact that pred (m + n) = pred m + n is definitionally true, this case is also proven.
\end{proof}

\begin{listing}[H]
\begin{minted}{agda}
+-succ : ∀ m n → m + succ n ≡ succ (m + n)
+-succ = ℤₕ-ind-prop
  (λ _ → isPropΠ λ _ → isSetℤₕ _ _)
  (λ m → refl)
  (λ m p n → cong succ (p n))
  (λ m p n → cong pred (p n) ∙ sec (m + n) ∙ sym (ret (m + n)))
\end{minted}
\caption{Agda proof of moving succ around addition}
\end{listing}

\begin{theorem}
  The pred constructor can be moved around addition: ∀ m, n ∈ ℤ: m + pred n = pred (m + n)
\end{theorem}

\begin{proof}
  Using our induction property:

  For the base case (m = 0): 0 + (pred n) = pred (0 + n) is a reflection.

  For the succ case: (succ m) + (pred n) = pred ((succ m) + n), since we have the original equality, we can apply succ on both sides of it. Unfortunately, this won't be enough, since this way we have the equation of succ (m + pred n) = succ (pred (m + n)), our right-hand sides don't match. We will have to use transitivity to change the right-hand side of the equation. We know that our retraction constructor states that succ (pred m) = m, applying this with 'm + n' gets rid of the succ (pred (...)) part, resulting in succ (m + pred n) = m + n. Using another transitivity, we will have to somehow put a pred (succ (...)) to this 'm + n'. This is just going to be the symmetric version of our section constructor, which states that pred (succ (m)) = m. This results in succ (m + pred n) = pred (succ (m + n)), thanks to the fact that succ (m + n) = succ m + n is definitionally true, this case is proven.

  For the pred case: (pred m) + (pred n) = pred ((pred m) + n), since we have the original equality of m + (pred n) = pred (m + n), it is enough to apply pred on both sides of the original equality, this results in pred (m + pred n) = pred (pred (m + n)), thanks to the fact that pred (m + n) = pred m + n is definitionally true, this case is also proven.
\end{proof}

\begin{listing}[H]
\begin{minted}{agda}
+-pred : ∀ m n → m + pred n ≡ pred (m + n)
+-pred = ℤₕ-ind-prop
  (λ _ → isPropΠ λ _ → isSetℤₕ _ _)
  (λ m → refl)
  (λ m p n → cong succ (p n) ∙ ret (m + n) ∙ sym (sec (m + n)))
  (λ m p n → cong pred (p n))
\end{minted}
\caption{Agda proof of moving pred around addition}
\end{listing}

\begin{theorem}
  Left inverse element exists for addition: ∀ z ∈ ℤ, ∃ inv ∈ ℤ: inv + z = 0
\end{theorem}

\begin{proof}
  In Agda, we will rewrite the statement as such: ∀ z ∈ ℤ → (- z) + z = 0, since we know that the inverse element is the negated number.

  For the base case (z = 0): (- 0) + 0 = 0 is a reflection.

  For the succ case: (- (succ z)) + (succ z) = 0. Definitionally, we know that - (succ m) = pred (- m), we can use this fact, by applying pred to the previously proven +-succ, with '- z' and 'z' parameters. This results in the equation pred ((- z) + succ z) ≡ pred (succ ((- z) + z)). Due to the previous note and the fact that definitionally pred (m + n) = pred m + n, our left hand side is correct. Using transitivity, we can get rid of the pred (succ (...)) part on the right-hand side by using the section constructor, resulting in pred ((- z) + succ z) ≡ (- z) + z. Using another transivitiy, our remaining right-hand side is just our original equation. Applying it, this case is proven.

  For the pred case: (- (pred z)) + (pred z) = 0. Definitionally, we know that - (pred m) = succ (- m), we can use this fact, by applying pred to the previously proven +-pred, with '- z' and 'z' parameters. This results in the equation succ ((- z) + pred z) ≡ succ (pred ((- z) + z)). Due to the previous note and the fact that definitionally succ (m + n) = succ m + n, our left hand side is correct. Using transitivity, we can get rid of the succ (pred (...)) part on the right-hand side by using the retraction constructor, resulting in succ ((- z) + pred z) ≡ (- z) + z. Using another transivitiy, our remaining right-hand side is just our original equation. Applying it, this case is also proven.
\end{proof}

\begin{listing}[H]
\begin{minted}{agda}
+-invˡ : ∀ z → (- z) + z ≡ zero
+-invˡ = ℤₕ-ind-prop
  (λ _ → isSetℤₕ _ _)
  refl
  (λ z p → cong pred (+-succ (- z) z) ∙ sec _ ∙ p)
  (λ z p → cong succ (+-pred (- z) z) ∙ ret _ ∙ p)
\end{minted}
\caption{Agda proof of addition having a left inverse element}
\end{listing}

\begin{theorem}
  Right inverse element exists for addition: ∀ z ∈ ℤ, ∃ inv ∈ ℤ: inv + z = 0
\end{theorem}

\begin{proof}
  As in the case of the left identity element, we will rewrite this statement in Agda as such: ∀ z ∈ ℤ → z + (- z) = 0.

  For the base case (z = 0): 0 + (- 0) = 0 is once again a reflection.

  For the succ case: (succ z) + (- (succ z)) = 0. Definitionally, we know that - (succ m) = pred (- m), we can use this fact, by applying succ to the previously proven +-pred, with 'z' and '- z' parameters. This results in the equation succ (z + pred (- z)) ≡ succ (pred (z + (- z))). Due to the previous note and the fact that definitionally succ (m + n) = succ m + n, our left hand side is correct. Using transitivity, we can get rid of the succ (pred (...)) part on the right-hand side by using the retraction constructor, resulting in succ (z + pred (- z)) ≡ z + (- z). Using another transivitiy, our remaining right-hand side is just our original equation. Applying it, this case is proven.

  For the pred case: (pred z) + (- (pred z)) = 0. Definitionally, we know that - (pred m) = succ (- m), we can use this fact, by applying pred to the previously proven +-succ, with 'z' and '- z' parameters. This results in the equation pred (z + succ (- z)) ≡ pred (succ (z + (- z))). Due to the previous note and the fact that definitionally pred (m + n) = pred m + n, our left hand side is correct. Using transitivity, we can get rid of the pred (succ (...)) part on the right-hand side by using the section constructor, resulting in pred (z + succ (- z)) ≡ z + (- z). Using another transivitiy, our remaining right-hand side is just our original equation. Applying it, this case is also proven.
\end{proof}

\begin{listing}[H]
\begin{minted}{agda}
+-invʳ : ∀ z → z + (- z) ≡ zero
+-invʳ = ℤₕ-ind-prop
  (λ _ → isSetℤₕ _ _)
  refl
  (λ z p → cong succ (+-pred z (- z)) ∙ ret _ ∙ p)
  (λ z p → cong pred (+-succ z (- z)) ∙ sec _ ∙ p)
\end{minted}
\caption{Agda proof of addition having a right inverse element}
\end{listing}

\section{Commutativity}
\begin{minted}[fontsize=\small]{agda}
+-comm : ∀ m n → m + n ≡ n + m
+-comm m n = +-comm' n m
  where
  +-comm' : ∀ n m → m + n ≡ n + m
  +-comm' = ℤₕ-ind-prop
    (λ _ → isPropΠ λ _ → isSetℤₕ _ _)
    +-zero
    (λ n p m → +-succ m n ∙ cong succ (p m))
    (λ n p m → +-pred m n ∙ cong pred (p m))
\end{minted}
