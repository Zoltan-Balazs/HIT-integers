\chapter{Induction Principle of HIT Integers}
\label{ch:indprin}

Before proving that HIT integers form a set, we will define some helper functions which will make it much easier for us to define operations on our type (iterator), as well make it fairly trivial to prove the needed properties for a set to form a commutative ring (induction property). Let us define the induction property first, to make this easier, we will define a helper property, the induction principle (otherwise known as the eliminator).

\section{Induction principle (eliminator)}

Defining the induction principle will be fairly easy, given that we have a correct type definition:
\begin{listing}[H]
\begin{minted}{agda}
ℤₕ-ind :
  ∀ {ℓ} {P : ℤₕ → Type ℓ}
  → (P-zero : P zero)
  → (P-succ : ∀ z → P z → P (succ z))
  → (P-pred : ∀ z → P z → P (pred z))
  → (P-sec : ∀ z → (pz : P z) →
           PathP
             (λ i → P (sec z i))
             (P-pred (succ z) (P-succ z pz))
             pz)
  → (P-ret : ∀ z → (pz : P z) →
           PathP
             (λ i → P (ret z i))
             (P-succ (pred z) (P-pred z pz))
             pz)
  → (P-coh : ∀ z → (pz : P z) →
           SquareP
             (λ i j → P (coh z i j))
             (congP (λ i → P-succ (sec z i)) (P-sec z pz))
             (P-ret (succ z) (P-succ z pz))
             refl
             refl)
  → (z : ℤₕ)
  → P z
\end{minted}
\caption{Agda type definition of the induction principle}
\end{listing}

The definition is fairly trivial, we will just need to pattern match on the given integer and use recursion:
\begin{listing}[H]
\begin{minted}{agda}
ℤₕ-ind P-zero P-succ P-pred P-sec P-ret P-coh zero        = P-zero
ℤₕ-ind P-zero P-succ P-pred P-sec P-ret P-coh (succ z)    = P-succ z (ℤₕ-ind P-zero P-succ P-pred P-sec P-ret P-coh z)
ℤₕ-ind P-zero P-succ P-pred P-sec P-ret P-coh (pred z)    = P-pred z (ℤₕ-ind P-zero P-succ P-pred P-sec P-ret P-coh z)
ℤₕ-ind P-zero P-succ P-pred P-sec P-ret P-coh (sec z i)   = P-sec z (ℤₕ-ind P-zero P-succ P-pred P-sec P-ret P-coh z) i
ℤₕ-ind P-zero P-succ P-pred P-sec P-ret P-coh (ret z i)   = P-ret z (ℤₕ-ind P-zero P-succ P-pred P-sec P-ret P-coh z) i
ℤₕ-ind P-zero P-succ P-pred P-sec P-ret P-coh (coh z i j) = P-coh z (ℤₕ-ind P-zero P-succ P-pred P-sec P-ret P-coh z) i j
\end{minted}
\caption{Agda code for the induction principle}
\end{listing}

\section{Induction property}

With the induction principle, defining the induction property is easy. The induction property will allow us to only prove the upcoming commutative ring properties for the base element and the 0-dimensional constructors ('succ' and 'pred'), for the 1- and 2-dimensional constructors ('sec', 'ret' and 'coh') the proofs will be induced from the 'succ' and 'pred' proofs:
\begin{listing}[H]
\begin{minted}{agda}
ℤₕ-ind-prop :
  ∀ {ℓ} {P : ℤₕ → Type ℓ}
  → (∀ z → isProp (P z))
  → P zero
  → (∀ z → P z → P (succ z))
  → (∀ z → P z → P (pred z))
  → (z : ℤₕ)
  → P z
ℤₕ-ind-prop {P = P} P-isProp P-zero P-succ P-pred =
  ℤₕ-ind
    P-zero
    P-succ
    P-pred
    (λ z pz → toPathP (P-isProp z _ _))
    (λ z pz → toPathP (P-isProp z _ _))
    (λ z pz → isProp→SquareP (λ i j → P-isProp (coh z i j)) _ _ _ _)
\end{minted}
\caption{Agda code for the induction property}
\end{listing}
(Note: We use the fact that Agda can infer the needed arguments for 'P-isProp', we can also manually give these parameters, but this would only lengthen our definition. See the source file for the manually given parameters.)
