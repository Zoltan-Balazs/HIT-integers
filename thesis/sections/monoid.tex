\chapter{Monoid (Multiplication)}
\label{ch:monoid}

\section{Multiplication Operation}
To define the multiplication operation, we will once again use our iterator. In this case however, we will have to do a bit of extra work. We cannot provide an Isomorphism in such an easy way as we did with addition. From elementary grade mathmatics, we know that multiplication is just repeated addition, for example if we want to calculate '3 * 4', we would do '4 + 4 + 4', or adding together '4' (right element) '3' (left element) times. So our main idea is to start with zero, and repeat the addition of the right parameter exactly the number of left parameter times (if the left parameter is negative, we will just add the inverted right parameter the absolute value of left parameter times).

As before, our function of having this desired effect will be defining the 'function' of isomorphism as 'z +', conversely, we can define the 'inverse function' as adding the negated value of this 'z': '- z +' since this will invert the effect of adding z to a number.

Next, we will need to prove the 'retraction' property of this.
\begin{theorem}
  Adding a negated value to a number, then the non-negated value of the same value, we get back the original number: ∀ m, n ∈ ℤ: n + ((- n) + m) = m
\end{theorem}

\begin{proof}
  To prove this, we will first use the associative property of addition to match the left-hand side. Supplying 'n', '- n' and 'm' results in the following equation: n + ((- n) + m) ≡ n + (- n) + m. We use transitivity to change the right-hand side of the equation. Getting under the '+ m' part, we can apply the existence of the right inverse element of addition with 'n' parameter. This will replace 'n + (- n)' with 0: n + ((- n) + m) ≡ 0 + m. We know that definitionally 0 + m = m, this property is proven.
\end{proof}

Afterwards, we will have to prove the 'section' property of this.
\begin{theorem}
  Adding a non-negated value to a number, then the negated value of the same value, we get back the original number: ∀ m, n ∈ ℤ: (- n) + (n + m) = m
\end{theorem}

\begin{proof}
  Similarly to the previous proof, to prove this, we will first use the associative property of addition to match the left-hand side. Supplying '- n', 'n' and 'm' results in the following equation: (- n) + (n + m) ≡ (- n) + n + m. We use transitivity to change the right-hand side of the equation. Getting under the '+ m' part, we can apply the existence of the left inverse element of addition with 'n' parameter. This will replace '(- n) + n' with 0: (- n) + (n + m) ≡ 0 + m. We know that definitionally 0 + m = m, this property is also proven.
\end{proof}

Next, we can convert this isomorphism to the proof that 'z +' is an equivalence ('isEquiv'), since we will need to supply parameters this time (instead of placing the constructors), we cannot directly convert to the equivalence ('Equiv'). Afterwards we will define our ordered pair of the equivalence, with a parameter this time.

Lastly, when defining our operation, we will use our previously mentioned idea of placing 'n +' (or '- n +') 'm' times on zero.

\begin{minted}{agda}
Iso-n+-ℤₕ : (z : ℤₕ) → Iso ℤₕ ℤₕ
Iso.fun      (Iso-n+-ℤₕ z)   = z +_
Iso.inv      (Iso-n+-ℤₕ z)   = - z +_
Iso.rightInv (Iso-n+-ℤₕ n) m = +-assoc n (- n) m ∙ cong (_+ m) (+-invʳ n)
Iso.leftInv  (Iso-n+-ℤₕ n) m = +-assoc (- n) n m ∙ cong (_+ m) (+-invˡ n)

isEquiv-n+-ℤₕ : ∀ z → isEquiv (z +_)
isEquiv-n+-ℤₕ z = isoToIsEquiv (Iso-n+-ℤₕ z)

Equiv-n+-ℤₕ : (z : ℤₕ) → ℤₕ ≃ ℤₕ
Equiv-n+-ℤₕ z = z +_ , isEquiv-n+-ℤₕ z

_*_ : ℤₕ → ℤₕ → ℤₕ
m * n = ℤₕ-ite zero (Equiv-n+-ℤₕ n) m
\end{minted}

Definitionally, the following hold true for the multiplication operation in Agda:
\begin{minted}{agda}
zero   * n ≡ zero
succ m * n ≡ n + m * n
pred m * n ≡ (- n) + m * n
\end{minted}
Note that the symmetric version of these also holds true.

We could once again try to define multiplication by pattern matching. In this case however, even defining 'sec' is tricky.
% !!BOUNDARY!!
\begin{minted}{agda}
_*_ : ℤₕ → ℤₕ → ℤₕ
zero      * b = zero
succ a    * b = a * b + b
pred a    * b = a * b - b
sec a i   * b = ?
ret a i   * b = ?
coh a i j * b = ?
\end{minted}

\begin{listing}[H]
\section{Identity Element}

\begin{theorem}
  Left identity element exists for multiplication: ∃ id ∈ ℤ, ∀ z ∈ ℤ: id * z = z
\end{theorem}

\begin{proof}
  In Agda, we will rewrite the statement as such: ∀ z ∈ ℤ → 1 * z = z, since we know that the identity element is 1.

  1 * z = z + 0, so we will reuse our theorem of proving that a right identity element exists for addition.
\end{proof}

\begin{minted}{agda}
*-idˡ : ∀ z → succ zero * z ≡ z
*-idˡ = +-idʳ
\end{minted}
\caption{Agda proof of multiplication having a left identity element}
\end{listing}

\begin{theorem}
  Right identity element exists for multiplication: ∃ id ∈ ℤ, ∀ z ∈ ℤ: z * id = z
\end{theorem}

\begin{proof}
  As in the case of the left identity element, we will rewrite this statement in Agda as such: ∀ z ∈ ℤ → z * 1 = z, since our right identity element is also going to be 1.

  For this, we won't use our induction property. Instead, we will rewrite the left-hand side of the equation to 1 * z, using the commutative property of multiplication (which we prove later on). Now we will need to prove that 1 * z = z, which is the proof of the left identity element existing.
\end{proof}

\begin{listing}[H]
\begin{minted}{agda}
*-idʳ : ∀ z → z * succ zero ≡ z
*-idʳ z = *-comm z (succ zero) ∙ *-idˡ z
\end{minted}
\caption{Agda proof of multiplication having a right identity element}
\end{listing}

\section{Multiplication Distributes over Addition}
As was the case for the identity element and inverse element proofs, we will have to prove both the left and right side for distributivity.

\begin{theorem}
  Multiplication is distributive over addition on the left side: ∀ m, n, o ∈ ℤ: (m + n) * o = (m * o) + (n * o)
\end{theorem}

\begin{proof}
  Using our induction property:

  For the base case (m = 0) : (0 + n) * o = (0 * o) + (n * o) is a reflection.

  For the succ case: ((succ m) + n) * o = ((succ m) * o) + (n * o). Definitionally, we know that (succ m) * o = o + m * o, as such, our equation is essentially the same as: o + (m * n) * o = o + (m * o) + (n * o). For the left-hand side of the equation, we will note that disregarding the beginning o + part, the equation is the same as the original equality's left-hand side, using congruence, we can substitute on the left-hand side. Using this, our right-hand side will look like this: o + (m * o + n * o), this is almost the exact thing that is on the right-hand side, apart from the fact that addition is paranthesized from the left (our original o + (m * n) + (n * o) = (o + (m * n)) + (n * o)), using the associative property of addition, we can rearrange the parts to be correctly parenthesized.

  For the pred case: ((pred m) + n) * o = ((pred m) * o) + (n * o). Once again, definitionally we know that (pred m) * o = (- o) + m * o. We can rewrite the equation once again as: - o + (m * n) * o = - o + (m * o) + (n * o). We can use the exact same steps as in the succ case (Using transitivity, rewriting the left-hand side using congruence and the original equality, rewriting the right hand side with the associative property of addition), noting the difference that for congruence and the first part of the associative property of addition, we will have to provide '- o' as a parameter instead of 'o'.
\end{proof}

\begin{listing}[H]
\begin{minted}{agda}
*-distribˡ-+ : ∀ m n o → (m + n) * o ≡ (m * o) + (n * o)
*-distribˡ-+ = ℤₕ-ind-prop
  (λ _ → isPropΠ2 λ _ _ → isSetℤₕ _ _)
  (λ n o → refl)
  (λ m p n o → cong (o +_) (p n o) ∙ +-assoc o (m * o) (n * o))
  (λ m p n o → cong (- o +_) (p n o) ∙ +-assoc (- o) (m * o) (n * o))
\end{minted}
\caption{Agda proof of multiplication being left distributive to addition}
\end{listing}

\begin{theorem}
  Multiplication is distributive over addition on the right side: ∀ m, n, o ∈ ℤ: m * (n + o) = (m * n) + (m * o)
\end{theorem}

\begin{proof}
  Using the commutative property of multiplication, we can rewrite m * (n + o) to (n + o) * m, this equation looks the same as the left-hand side our previous theorem (multiplication is distributive over addition on the left side). Applying it, with the correct parameters (n, o, m as 'm', 'n' and 'o' respectively), we get the equation of n * m + o * m. These are the exact parts that we need, we will just need to use the commutative property of multiplication twice to switch around the terms. (Note: we can use the congruences in any way, i.e. it doesn't matter if the rearrange the left or the right part first.)
\end{proof}

\begin{listing}[H]
\begin{minted}{agda}
*-distribʳ-+ : ∀ m n o → m * (n + o) ≡ (m * n) + (m * o)
*-distribʳ-+ m n o = *-comm m (n + o) ∙ *-distribˡ-+ n o m ∙ cong (n * m +_) (*-comm o m) ∙ cong (_+ m * o) (*-comm n m)
\end{minted}
\caption{Agda proof of multiplication being right distributive to addition}
\end{listing}

\section{Associative}
Before proving that multiplication is associative, we will prove 3 helper theorems.

\begin{theorem}
  Negating an addition is equal to negating the parts separately, and then adding them together. ∀ m, n ∈ ℤ: - (m + n) = (- m) + (- n)
\end{theorem}

\begin{proof}
  Using our induction property:

  For the base case (m = 0): - (0 + n) = (- 0) + (- n) is a reflection.

  For the succ case: - ((succ m) + n) = (- (succ m)) + (- n). Note that we have our original equality of - (m + n) = (- m) + (- n). Definitonally - (succ m) = pred (- m), since it doesn't matter if we increase the value, then negate it, or if we negate it and decrement it. Rewriting our case this way results in: pred (- (m + n)) = (pred (- m)) + (-n), that we need to prove. This way, applying pred to both sides of the original equality, we prove this case.

  For the pred case: - ((pred m) + n) = (- (pred m)) + (- n). Similarly to the succ case, we have our original equality. Definitionally - (pred m) = succ (- m). Rewriting our case this way results in: succ (- (m + n)) = (succ (- m)) + (- n), that we need to prove. So, similarly to the succ case, we can apply succ to both sides of the original equality, proving this case as well.
\end{proof}

\begin{minted}{agda}
inv-hom-ℤₕ : ∀ m n → - (m + n) ≡ (- m) + (- n)
inv-hom-ℤₕ = ℤₕ-ind-prop
  (λ _ → isPropΠ λ _ → isSetℤₕ _ _)
  (λ n → refl)
  (λ m p n → cong pred (p n))
  (λ m p n → cong succ (p n))
\end{minted}

\begin{theorem}
  Multiplying a number ('m') by a right negated number ('- n') is the same as multiplying the non-negated numbers first ('m * n'), and negating the result. ∀ m, n ∈ ℤ: m * (- n) = - (m * n)
\end{theorem}

\begin{proof}
  Using our induction property:

  For the base case (m = 0): 0 * (- n) = - (0 * n) is a reflection.

  For the succ case: (succ m) * (- n) = - ((succ m) * n). Definitionally succ m * (- n) = (- n) + m * (- n). The m * (- n) part is the left-hand side of our original equality, if we use congruence to get under the '(- n)' beginning part, we can re-use our original equality with 'n' parameter being supplied. This results in the equality (- n) + m * (- n) = (- n) + (- (m * n)). Afterwards, using transitivity, to change the right-hand side, we will also note, since we proved inv-hom, that we can rewrite '(- n) + (- (m * n))' as '- (n + m * n)', if we use the symmetric version of our previously proven inv-hom property and supply 'n' to the m parameter, and 'm * n' to the n parameter. This will result in the equation (- n) + m * (- n) = (- (n + m * n)). We will note, once again, that definitionally succ m * n = n + m * n, so the right-hand side is also what we need to prove.

  For the pred case: (pred m) * (- n) = - ((pred m) * n). Similarly to the 'succ' case, we note that definitionally pred m * (- n) = (- (- n)) + m * (- n) and pred m * n = (- n) + m * n hold true. In a similar chain of thought, we can use congruence to get under the '(- (- n))' part, we can re-use our original equality with 'n' parameter supplied to it. Afterwards we can use transitivity, and the previously proven inv-hom, specifically the symmetric version of it, with '- n' supplied to the m parameter, and 'm * n' supplied to the n parameter to rewrite our right-hand side. This results in the equation (- (- n)) + m * (- n) = (- ((- n) + m * n)), where both left- and right-hand sides are definitionally equal to the equation we need to prove.
\end{proof}

\begin{minted}{agda}
*-inv : ∀ m n → m * (- n) ≡ - (m * n)
*-inv = ℤₕ-ind-prop
  (λ _ → isPropΠ λ _ → isSetℤₕ _ _)
  (λ n → refl)
  (λ m p n → cong (- n +_) (p n) ∙ sym (inv-hom-ℤₕ n (m * n)))
  (λ m p n → cong (- (- n) +_) (p n) ∙ sym (inv-hom-ℤₕ (- n) (m * n)))
\end{minted}

\begin{theorem}
  Similarly, multiplying a left negated number ('- m') by a number ('n') is the same as multiplying the non-negated numbers first ('m * n'), and negating the result: ∀ m, n ∈ ℤ: (- m) * n = - (m * n).
\end{theorem}

\begin{proof}
  Using the commutative property of multiplication, we can rewrite the left-hand side of our equation: (- m) * n = n * (- m). Using transitivity, we can continue changing the right-hand side of the equation. Our previously proved multiplication-negation relation has the same left-hand side that we need to change, applying it (with the 'm' parameter being 'n' and the 'n' parameter being 'm') we get the following equation: (- m) * n = - (n * m). Once again, we need to use transtivity to keep changing the right-hand side of the equation. If we use the commutative property of multiplication under the negation operation, we can rewrite the right-hand side of the equation to match the equation we need to prove.
\end{proof}

\begin{minted}{agda}
inv-* : ∀ m n → (- m) * n ≡ - (m * n)
inv-* m n = *-comm (- m) n ∙ *-inv n m ∙ cong (-_) (*-comm n m)
\end{minted}

\begin{theorem}
  Multiplication is associative: ∀ m, n, o ∈ ℤ: m * (n * o) = (m * n) * o
\end{theorem}

\begin{proof}
  Using our induction property:

  For the base case (m = 0): 0 * (n * o) = (0 * n) * o is a reflection.

  For the succ case: (succ m) * (n * o) = ((succ m) * n) * o. Definitionally, we know that (succ m) * n = n + m * n, With this, we can rewrite our equation to: (n * o) + m * (n * o) = (n + m * n) * o. Note, that on the left-hand side, apart from the '(n * o)' part, we have our original equality's left-hand side. If we use congruence to get under this '(n * o)' part, we can apply the original equality (with 'n' and 'o' parameters supplied respectively). This changes our equation to: (n * o) + m * (n * o) = (n * o) + (m * n) * o, using transitivity, we can change the right-hand side. We note that that if we use the symmetric version of multiplication distributing over addition on the left hand side, with 'n', 'm * n' and 'o' supplied, we have the property of: (n * o) + ((m * n) * o) = (n + (m * n)) * o. The order of parentheses are correct, so this case is proven.

  For the pred case: (pred m) * (n * o) = ((pred m) * n) * o. Once again, we know definitionally that (pred m) * n = (- n) + m * n. Using this, we can rewrite the equation to: - (n * o) + m * (n * o) = ((- n) + m * n) * o. Once again, on the left-hand side, apart from the '- (n * o)' part, we have our original equality's left-hand side. We can use congruence once again to get under this part, and apply the original equality (with the same 'n' and 'o' parameters). Using this, our equation becomes: - (n * o) + m * (n * o) = - (n * o) + (m * n) * o. We will need to use transtivity once again to change the right-hand side of the equation. Unlike last time, we cannot use the left distributive property of multiplication over addition yet, since we cannot match the parameters correctly just yet. If we first instead use our previously proven 'inv-*' property (before first getting under the '+ m * n * o' part - note that since multiplication is parenthesized from the left, (m * n) * o and m * n * o are equal), specifically the symmetric version of it (with 'n' and 'o' parameters supplied), we can change our equation to: - (n * o) + m * (n * o) = (- n) * o + (m * n) * o. Using another transitivity, we can now use the left distributive property of multiplication over addition (again, the symmetric version of it), with '(- n)', 'm * n' and 'o' parameters supplied. With this, our equation becomes: - (n * o) + m * (n * o) = ((- n) + (m * n)) * o. Once again, thanks to the order of parentheses is correct, this case is also proven.
\end{proof}

\begin{listing}[H]
\begin{minted}{agda}
*-assoc : ∀ m n o → m * (n * o) ≡ (m * n) * o
*-assoc = ℤₕ-ind-prop
  (λ _ → isPropΠ2 λ _ _ → isSetℤₕ _ _)
  (λ n o → refl)
  (λ m p n o → cong (n * o +_) (p n o) ∙ sym (*-distribˡ-+ n (m * n) o))
  (λ m p n o → cong (- (n * o) +_) (p n o) ∙ cong (_+ m * n * o) (sym (inv-* n o)) ∙ sym (*-distribˡ-+ (- n) (m * n) o))
\end{minted}
\caption{Agda proof of multiplication being associative}
\end{listing}
