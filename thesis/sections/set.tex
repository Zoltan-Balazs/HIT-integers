\chapter{Proving that HIT Integers Form a Set}
\label{ch:set}
This will be the first quite labours property to define. To make our live easier in the future, we will have to prove that our definition of integers actually form a set. To prove this, we will prove that our definition of integers is isomorphic with the standard definition of integers in cubical Agda:
\begin{minted}{agda}
data ℤ : Type₀ where
  pos    : (n : ℕ) → ℤ
  negsuc : (n : ℕ) → ℤ
\end{minted}
To do this, we will need to define 4 functions:
\begin{compactitem}
  \item We can convert our type to the standard integer definition
  \item We can convert from the standard integer definition to our type
  \item Converting the standard integer definition to our type, and back to the standard integer definition results in the exact same value
  \item Converting our type to the standard integer definition, and back to our type results in the exact same value
\end{compactitem}
This is a sort of pseudo comparison of the standard integer type and our type as well, if we prove that our type is isomorphic with the standard integer type, then the two types are equivalent as well.

Note: We could also go the way of defining that our HIT integers are discrete. One of the main points of the thesis is to compare different integer definitions to the HIT integers, so we will try to do less work and prove that our HIT integers form a set while comparing them to the standard integer definition. This also brings the quite fortunate side-effect that any other integer definitions that also prove isomorphism with the standard integers (in the cubical library one notable example is the previously mentioned bi-invertible integers) also, by extension, are isomorphic with our HIT integers.

Let us begin with proving the equivalence between standard integers and HIT integers.

\section{Converting our type to the standard definition}
First off, we will define the function to convert from our type to the standard integer type. We will do so by pattern matching on our integer value. This will give us all the cases for our constructors, for which we will need to provide an equivalent standard integer value. For our 'zero' value, the equivalent standard integer value is 'pos zero'. For our 'succ z' case, we will need to increment the value with 'sucℤ' and recursively convert the rest by calling our conversion function with 'z'. 'sucℤ' is defined for the standard integers, its purpose is to increment the standard integer value by one. For our 'pred z' case we will do the same thing, just with 'predℤ', as one might imagine, 'predℤ' is responsible for decrementing the standard integer value by one. The next case is the first truly interesting one, as we will have to convert a 'sec' integer to a standard integer value. As previously mentioned 'sec' is responsible for the equivalence of \mintinline{agda}{pred (succ z) ≡ z}. As such, we will not only need to give an appropriate standard integer value, but also satisfy the boundaries as stated by Agda:
\begin{minted}{agda}
i = i0 ⊢ predℤ (sucℤ (ℤₕ-ℤ z))
i = i1 ⊢ ℤₕ-ℤ z
\end{minted}
These boundaries match what our 'sec' constructor state, just for the standard integer values. (Note that instead of our integer type, we are dealing with the converted standard integer values, as such, we are basically proving that \mintinline{agda}{predℤ (sucℤ (ℤₕ-ℤ z)) ≡ ℤₕ-ℤ z}). Thankfully for us, there is a function in the standard library for this exact case called 'predSuc', this provides the exact boundary we need: \mintinline{agda}{∀ z → predℤ (sucℤ z) ≡ z}, as before, we will need to recursively call with the converted remaining integer value, as well as to pass the boundary 'i', as without that, it is just an equivalence, but giving the 'i' boundary resolves it to a standard integer value.
This is also the case for our 'ret' constructor, we will just need to use the standard 'sucPred' function, which is exactly what we need to satisfy the boundaries. (The wanted boundary is also different, to reflect the fact that we need to prove that 'ret' is defined as \mintinline{agda}{succ (pred z) ≡ z}.)
Our last constructor is 'coh', for which we will need a standard integer value and satisfy the needed boundaries. We will define the 'coh' rule for the standard integers. Following the cubical library, we will pattern match, do cover the 'pos n', 'negsuc zero' and 'negsuc (suc n)' cases. All of these cases result in a reflection, thanks to the way that 'sucℤ', 'predSuc' and 'sucPred' are defined.
\begin{minted}{agda}
cohℤ : ∀ z → congS sucℤ (predSuc z) ≡ sucPred (sucℤ z)
cohℤ (pos n)          = refl
cohℤ (negsuc zero)    = refl
cohℤ (negsuc (suc n)) = refl
\end{minted}

Note: the wanted boundaries for the conversion's 'coh' branch are as such:
\begin{minted}{agda}
j = i0 ⊢ sucℤ (predℤ (sucℤ (ℤₕ→ℤ z)))
j = i1 ⊢ sucℤ (ℤₕ→ℤ z)
i = i0 ⊢ sucℤ (predSuc (ℤₕ→ℤ z) j)
i = i1 ⊢ sucPred (sucℤ (ℤₕ→ℤ z)) j
\end{minted}
these exactly correspond to what the 4 parts of our 'coh' rule are, just with the standard integer functions and values. When using the 'coh' rule defined for the standard integers, we will have to convert our HIT integer to a standard integer, as well as providez both of our original boundaries.

Rewriting these wanted boundaries in Agda to a Square results in the following:
\[
\begin{tikzcd}
	\bullet &&&& \bullet \\
	\\
	\\
	\\
	\bullet &&&& \bullet
	\arrow["{suc\mathbb{Z}\:(\mathbb{Z}_h\shorttextrightarrow\mathbb{Z}\:z)}"{description}, no head, from=1-1, to=1-5]
	\arrow["{suc\mathbb{Z}\:(predSuc\:(\mathbb{Z}_h\shorttextrightarrow\mathbb{Z}\:z)\:j)}"{description}, no head, from=5-1, to=1-1]
	\arrow["{suc\mathbb{Z}\:(pred\mathbb{Z}\:(suc\mathbb{Z}\:(\mathbb{Z}_h\shorttextrightarrow\mathbb{Z}\:z)))}"{description}, no head, from=5-1, to=5-5]
	\arrow["{sucPred\:(suc\mathbb{Z}\:(\mathbb{Z}_h\shorttextrightarrow\mathbb{Z}\:z))\:j}"{description}, no head, from=5-5, to=1-5]
\end{tikzcd}
\]

\begin{listing}[H]
\begin{minted}{agda}
ℤₕ→ℤ : ℤₕ → ℤ
ℤₕ→ℤ zero        = pos zero
ℤₕ→ℤ (succ z)    = sucℤ (ℤₕ→ℤ z)
ℤₕ→ℤ (pred z)    = predℤ (ℤₕ→ℤ z)
ℤₕ→ℤ (sec z i)   = predSuc (ℤₕ→ℤ z) i
ℤₕ→ℤ (ret z i)   = sucPred (ℤₕ→ℤ z) i
ℤₕ→ℤ (coh z i j) = cohℤ (ℤₕ→ℤ z) i j
\end{minted}
\caption{Agda code for converting between HIT and the standard integers}
\end{listing}

\section{Converting the standard definition to our type}
Converting from the standard definition to our type turns out to be easier than the vice-versa. Pattern matching on both parameters (the default 'n' parameter, then the expanded 'pos n' and 'negsuc n' parameter) introduces four cases. We need to remember that 'pos zero' is equivalent to our 'zero' constructor, 'pos (suc n)' is equivalent to applying our 'succ' constructor, to the recursively converted 'pos n' value. 'negsuc' is an interesting case, as it is definitionally introduces '-1' to the value, so 'negsuc zero' is equivalent to '0 - 1', which is our 'pred zero', equivalently, 'negsuc (suc n)' is equivalent to applying 'pred' to the recursively converted 'negsuc n' value.
\begin{listing}[H]
\begin{minted}{agda}
ℤ→ℤₕ : ℤ → ℤₕ
ℤ→ℤₕ (pos zero)       = zero
ℤ→ℤₕ (pos (suc n))    = succ (ℤ→ℤₕ (pos n))
ℤ→ℤₕ (negsuc zero)    = pred zero
ℤ→ℤₕ (negsuc (suc n)) = pred (ℤ→ℤₕ (negsuc n))
\end{minted}
\caption{Agda code for converting between standard integers and HIT integers}
\end{listing}

\section{Converting the standard definition to our type and back}
Converting to and from isn't enough, we have to also ensure that if we are to convert a standard integer to our type, then that value in our type back to the standard integer, we get the same initial value.

As we did before, it is beneficial to pattern match to get the constructors of the standard integers. It turns out that if pattern match once again on the two constructors (so we get 'pos zero', 'pos (suc n)', 'negsuc zero' and 'negsuc (suc n)' cases), two of the four cases ('pos zero' and 'negsuc zero') become reflections. (Thanks to the way we defined 'ℤ→ℤₕ' and 'ℤₕ→ℤ' for the previously mentioned two cases, and 'zero' as well as 'pred zero')

Let's deal with the remaining two cases:

For 'pos (suc n)', we will have to prove the following: sucℤ (ℤₕ→ℤ (ℤ→ℤₕ (pos n))) ≡ pos (suc n). Note that more than likely we will need to recursively call the ℤ→ℤₕ→ℤ function with the remaining 'pos n' value, so let's do so. If we do this, suddenly we will already have the following: ℤₕ→ℤ (ℤ→ℤₕ (pos n)) ≡ pos n. Following the left-hand side, we will use congruence to apply the missing 'sucℤ' on both sides, this has the additional benefit that thanks to the definition of 'sucℤ', the right-hand side sucℤ (pos n) is definitionally equal to pos (suc n), the exact same thing we need.

For 'negsuc (suc n)' the story is quite similar. We need to prove the following: predℤ (ℤₕ→ℤ (ℤ→ℤₕ (negsuc n))) ≡ negsuc (suc n), we have the intuition of the recursive call with 'negsuc n' value, doing so results in ℤₕ→ℤ (ℤ→ℤₕ (negsuc n)) ≡ negsuc n, applying 'predℤ' to match the left-hand side brings the additional benefit that definitionally predℤ (negsuc n) is equal to negsuc (suc n), exactly what we need.

\begin{listing}[H]
\begin{minted}{agda}
ℤ→ℤₕ→ℤ : (z : ℤ) → ℤₕ→ℤ (ℤ→ℤₕ z) ≡ z
ℤ→ℤₕ→ℤ (pos zero)       = refl
ℤ→ℤₕ→ℤ (pos (suc n))    = cong sucℤ (ℤ→ℤₕ→ℤ (pos n))
ℤ→ℤₕ→ℤ (negsuc zero)    = refl
ℤ→ℤₕ→ℤ (negsuc (suc n)) = cong predℤ (ℤ→ℤₕ→ℤ (negsuc n))
\end{minted}
\caption{Agda proof that converting standard integers to HIT integers and back results in the same value}
\end{listing}

\section{Converting our type to the standard definition and back}
Conversely, we will have to prove the other back-and-forth as well. We will need to pattern match to prove cases for all of our HIT integer constructors.

For the 'zero' case, thankfully this is going to be a reflection. (Once again due to the way we defined 'ℤ→ℤₕ' and 'ℤₕ→ℤ'.)

For our 'succ' case, we will need to prove ℤ→ℤₕ (sucℤ (ℤₕ→ℤ z)) ≡ succ z. We will need to define the equivalence of first incerementing a standard integer with 'sucℤ' and converting it to HIT integer and first converting a standard integer to HIT integer and incrementing it with 'succ'. This is also going to be a fairly trivial proof, we will just need to use our 'retraction' constructor on the 'negsuc' cases (more specifically the symmetric version of it) with the standard integer value +1 converted:
\begin{minted}{agda}
ℤ-ℤₕ-sucℤ : (z : ℤ) → ℤ-ℤₕ (sucℤ z) ≡ succ (ℤ-ℤₕ z)
ℤ-ℤₕ-sucℤ (pos n)          = refl
ℤ-ℤₕ-sucℤ (negsuc zero)    = sym (ret (ℤ-ℤₕ (pos zero)))
ℤ-ℤₕ-sucℤ (negsuc (suc n)) = sym (ret (ℤ-ℤₕ (negsuc n)))
\end{minted}
Once we have this, we can using it, we get the following: ℤ→ℤₕ (sucℤ (ℤₕ→ℤ z)) ≡ succ (ℤ→ℤₕ (ℤₕ→ℤ z)), we can use a transitivity to rewrite the right-hand side with a lambda function (to introduce the boundary), since ℤ→ℤₕ (ℤₕ→ℤ z) is exactly what we are trying to prove is equal to z.

For our 'pred' case, we similarly have to prove that ℤ→ℤₕ (predℤ (ℤₕ→ℤ z)) ≡ pred z. We will once again introduce the same equivalence for decrementing with 'predℤ' and converting being the same as converting then decrementing with 'pred'. This is an even more trivial proof, we will to use the 'section' constructor on the 'pos (suc n)' case (again, the symmetric version of it) with the standard integer value -1 converted.
\begin{minted}{agda}
ℤ-ℤₕ-predℤ : (z : ℤ) → ℤ-ℤₕ (predℤ z) ≡ pred (ℤ-ℤₕ z)
ℤ-ℤₕ-predℤ (pos zero)    = refl
ℤ-ℤₕ-predℤ (pos (suc n)) = sym (sec (ℤ-ℤₕ (pos n)))
ℤ-ℤₕ-predℤ (negsuc n)    = refl
\end{minted}
This won't be enough, as using this, we get the following: ℤ→ℤₕ (predℤ (ℤₕ→ℤ z)) ≡ pred (ℤ→ℤₕ (ℤₕ→ℤ z)), using transitivity again, we can rewrite the right-hand side, since it should be equal (at least we are trying to prove it) to pred z.

We won't dive that deep into our 'sec' and 'ret' cases right now, do note that we will introduce a helper function to fill a square. The same proof is present in the standard library, when trying to prove that bi-invertible integers form a set. In the further work chapter, we will dive deeper into how the 'coh' case can be solved, the same idea is used when trying to prove 'sec' and 'ret'.

\begin{minted}{agda}
sym-filler : ∀ {ℓ} {A : Type ℓ} {x y : A} (p : x ≡ y)
                → Square (sym p)
                         refl
                         refl
                         p
sym-filler p i j = p (i ∨ ~ j)
\end{minted}

The 'sym-filler' helper function is used to fill a square, where 3 points are the same, and we know the equality 'x = y'.
\[\begin{tikzcd}
	x && x \\
	\\
	y && x
	\arrow["refl"', no head, from=1-1, to=1-3]
	\arrow["refl"', no head, from=1-3, to=3-3]
	\arrow["p"', no head, from=3-1, to=1-1]
	\arrow["{sym\;p}", no head, from=3-1, to=3-3]
\end{tikzcd}\]

To prove the 'sec' case, the following helper function is introduced:
\begin{minted}{agda}
ℤ→ℤₕ-predSuc : (x : ℤ)
              → Square (ℤ→ℤₕ-predℤ (sucℤ x) ∙ (λ i → pred (ℤ→ℤₕ-sucℤ x i)))
                       (λ _ → ℤ→ℤₕ x)
                       (λ i → ℤ→ℤₕ (predSuc x i))
                       (sec (ℤ→ℤₕ x))
ℤ→ℤₕ-predSuc (pos n) i j
  = hcomp (λ k → λ { (j = i0) → ℤ→ℤₕ (pos n)
                   ; (i = i0) → rUnit (sym (sec (ℤ→ℤₕ (pos n)))) k j
                   ; (i = i1) → ℤ→ℤₕ (pos n)
                   ; (j = i1) → sec (ℤ→ℤₕ (pos n)) i
                   })
          (sym-filler (sec (ℤ→ℤₕ (pos n))) i j)
ℤ→ℤₕ-predSuc (negsuc zero) i j
  = hcomp (λ k → λ { (j = i0) → ℤ→ℤₕ (negsuc zero)
                   ; (i = i0) → lUnit (λ i → pred (sym (ret (ℤ→ℤₕ (pos zero))) i)) k j
                   ; (i = i1) → ℤ→ℤₕ (negsuc zero)
                   ; (j = i1) → hoc (ℤ→ℤₕ (pos zero)) k i
                   })
          (pred (sym-filler (ret (ℤ→ℤₕ (pos zero))) i j))
ℤ→ℤₕ-predSuc (negsuc (suc n)) i j
  = hcomp (λ k → λ { (j = i0) → ℤ→ℤₕ (negsuc (suc n))
                   ; (i = i0) → lUnit (λ i → pred (sym (ret (ℤ→ℤₕ (negsuc n))) i)) k j
                   ; (i = i1) → ℤ→ℤₕ (negsuc (suc n))
                   ; (j = i1) → hoc (ℤ→ℤₕ (negsuc n)) k i
                   })
  (pred (sym-filler (ret (ℤ→ℤₕ (negsuc n))) i j))
\end{minted}

To prove the 'ret' case, the following helper function is introcuded:
\begin{minted}{agda}
ℤ→ℤₕ-sucPred : (z : ℤ)
              → Square (ℤ→ℤₕ-sucℤ (predℤ z) ∙ (λ j → succ (ℤ→ℤₕ-predℤ z j)))
                       (λ _ → ℤ→ℤₕ z)
                       (λ i → ℤ→ℤₕ (sucPred z i))
                       (ret (ℤ→ℤₕ z))
ℤ→ℤₕ-sucPred (pos zero) i j =
  hcomp (λ k → λ { (j = i0) → ℤ→ℤₕ (pos zero)
                 ; (i = i0) → rUnit (sym (ret (ℤ→ℤₕ (pos zero)))) k j
                 ; (i = i1) → ℤ→ℤₕ (pos zero)
                 ; (j = i1) → ret (ℤ→ℤₕ (pos zero)) i
                 })
        (sym-filler (ret (ℤ→ℤₕ (pos zero))) i j)
ℤ→ℤₕ-sucPred (pos (suc n)) i j =
  hcomp (λ k → λ { (j = i0) → succ (ℤ→ℤₕ (pos n))
                 ; (i = i0) → lUnit (λ i → succ (sym (sec (ℤ→ℤₕ (pos n))) i)) k j
                 ; (i = i1) → succ (ℤ→ℤₕ (pos n))
                 ; (j = i1) → coh (ℤ→ℤₕ (pos n)) k i
                 })
        (succ (sym-filler (sec (ℤ→ℤₕ (pos n))) i j))
ℤ→ℤₕ-sucPred (negsuc n) i j =
  hcomp (λ k → λ { (j = i0) → ℤ→ℤₕ (negsuc n)
                 ; (i = i0) → rUnit (sym (ret (ℤ→ℤₕ (negsuc n)))) k j
                 ; (i = i1) → ℤ→ℤₕ (negsuc n)
                 ; (j = i1) → ret (ℤ→ℤₕ (negsuc n)) i
                 })
        (sym-filler (ret (ℤ→ℤₕ (negsuc n))) i j)
\end{minted}

\begin{listing}[H]
\begin{minted}{agda}
ℤₕ-ℤ-ℤₕ zero          = refl
ℤₕ-ℤ-ℤₕ (succ z)      = ℤ-ℤₕ-sucℤ (ℤₕ-ℤ z) ∙ (λ i → succ (ℤₕ-ℤ-ℤₕ z i))
ℤₕ-ℤ-ℤₕ (pred z)      = ℤ-ℤₕ-predℤ (ℤₕ-ℤ z) ∙ (λ i → pred (ℤₕ-ℤ-ℤₕ z i))
ℤₕ-ℤ-ℤₕ (sec z i) j   =
  hcomp (λ k → λ { (j = i0) → ℤ-ℤₕ (predSuc (ℤₕ-ℤ z) i)
                 ; (i = i0) → (ℤ-ℤₕ-predℤ (sucℤ (ℤₕ-ℤ z)) ∙ (λ i → pred (compPath-filler (ℤ-ℤₕ-sucℤ (ℤₕ-ℤ z))
                   (λ i' → succ (ℤₕ-ℤ-ℤₕ z i'))
                   k i))) j
                 ; (i = i1) → ℤₕ-ℤ-ℤₕ z (j ∧ k)
                 ; (j = i1) → sec (ℤₕ-ℤ-ℤₕ z k) i })
        (ℤ-ℤₕ-predSuc (ℤₕ-ℤ z) i j)
ℤₕ-ℤ-ℤₕ (ret z i) j   =
  hcomp (λ k → λ { (j = i0) → ℤ-ℤₕ (sucPred (ℤₕ-ℤ z) i)
                 ; (i = i0) → (ℤ-ℤₕ-sucℤ (predℤ (ℤₕ-ℤ z)) ∙ (λ i → succ (compPath-filler (ℤ-ℤₕ-predℤ (ℤₕ-ℤ z))
                   (congS pred (ℤₕ-ℤ-ℤₕ z))
                   k i))) j
                 ; (i = i1) → ℤₕ-ℤ-ℤₕ z (j ∧ k)
                 ; (j = i1) → ret (ℤₕ-ℤ-ℤₕ z k) i  })
        (ℤ-ℤₕ-sucPred (ℤₕ-ℤ z) i j)
\end{minted}
\caption{Agda partial proof that converting HIT integers to standard integers and back results in the same value}
\end{listing}

\section{Set truncation}
With these 4 functions defined, we can prove that our type is isomorphic with the standard definition:
\begin{minted}{agda}
ℤ-iso : Iso ℤ ℤₕ
ℤ-iso .Iso.fun      = ℤ-ℤₕ
ℤ-iso .Iso.inv      = ℤₕ-ℤ
ℤ-iso .Iso.rightInv = ℤₕ-ℤ-ℤₕ
ℤ-iso .Iso.leftInv  = ℤ-ℤₕ-ℤ

ℤ≡ℤₕ : ℤ ≡ ℤₕ
ℤ≡ℤₕ = isoToPath ℤ-iso
\end{minted}
We pattern match on the constructors of 'Iso' (isomorphism) and we provide the needed fields. (As discussed earlier)

Finally, we can use the fact that the standard definition forms a set to our advantage, as our type is isomorphic with the standard definition means that our type also forms a set:
\begin{minted}{agda}
isSetℤₕ : isSet ℤₕ
isSetℤₕ = subst isSet ℤ≡ℤₕ isSetℤ
\end{minted}
